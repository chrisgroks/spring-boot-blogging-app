plugins {
    id 'org.springframework.boot' version '3.3.13'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id "com.netflix.dgs.codegen" version "6.2.1"
    id 'checkstyle'
}

version = '0.0.1-SNAPSHOT'
sourceCompatibility = '23'
targetCompatibility = '23'

checkstyle {
    toolVersion = '10.12.4'
    configFile = file('config/checkstyle/checkstyle.xml')
}

checkstyleMain {
    source = 'src/main/java'
}

checkstyleTest {
    source = 'src/test/java'
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'
    
    implementation platform('com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:9.1.2')
    implementation 'com.netflix.graphql.dgs:graphql-dgs-spring-graphql-starter'
    
    implementation 'org.flywaydb:flyway-core'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6',
                'io.jsonwebtoken:jjwt-jackson:0.12.6'
    implementation 'joda-time:joda-time:2.12.7'
    implementation 'org.xerial:sqlite-jdbc:3.47.1.0'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'io.rest-assured:rest-assured:5.5.0'
    testImplementation 'io.rest-assured:json-path:5.5.0'
    testImplementation 'io.rest-assured:xml-path:5.5.0'
    testImplementation 'io.rest-assured:spring-mock-mvc:5.5.0'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.4'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('clean') {
    doFirst {
        delete './dev.db'
    }
}

tasks.named('generateJava') {
    schemaPaths = ["${projectDir}/src/main/resources/schema"] // List of directories containing schema files
    packageName = 'io.spring.graphql' // The package name to use to generate sources
}
